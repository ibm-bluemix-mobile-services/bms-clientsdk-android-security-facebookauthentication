apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    mavenCentral()
    flatDir{
        dirs 'libs'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.facebook.android:facebook-android-sdk:4.5.0'
    compile group: 'com.ibm.mobilefirstplatform.clientsdk.android',
            name: 'core',
            version: '1.0.0',
            ext: 'aar',
            transitive: true
}

task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs

    exclude '**/BuildConfig.java'
    exclude '**/R.java'

    options.links 'http://docs.oracle.com/javase/7/docs/api/'

    options.encoding 'UTF-8'

    setTitle 'Bluemix Mobile Services Facebook OAuth Android API'
}

task androidJavadocsJar(type: Jar, dependsOn: generateJavadoc) {
    classifier = 'javadoc'
    from generateJavadoc.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

def getRepositoryUsername() {
    return hasProperty('ossrhUsername') ? ossrhUsername : ""
}

def getRepositoryPassword() {
    return hasProperty('ossrhPassword') ? ossrhPassword : ""
}

def getReleaseRepositoryUrl() {
    return 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
}

def getSnapshotRepositoryUrl() {
    return 'https://oss.sonatype.org/content/repositories/snapshots/'
}

uploadArchives {
    repositories {
        mavenDeployer {
//            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: getReleaseRepositoryUrl()) {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }

            snapshotRepository(url: getSnapshotRepositoryUrl()) {
                authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
            }

            pom.groupId='com.ibm.mobilefirstplatform.clientsdk.android'
            pom.artifactId='facebookauthentication'
            pom.version = '1.0.0-SNAPSHOT'

            pom.project {
                name 'IBM Bluemix Mobile Services - Client SDK Android Facebook Authentication'
                description 'This is the Facebook OAuth component of the Android SDK for IBM Bluemix Mobile Services.'
                url 'https://github.com/ibm-bluemix-mobile-services/bms-clientsdk-android-security-facebookauthentication'

                scm {
                    connection 'scm:git:git@github.com/ibm-bluemix-mobile-services/bms-clientsdk-android-security-facebookauthentication.git'
                    developerConnection 'scm:git:git@github.com/ibm-bluemix-mobile-services/bms-clientsdk-android-security-facebookauthentication.git'
                    url 'https://github.com/ibm-bluemix-mobile-services/bms-clientsdk-android-security-facebookauthentication.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}